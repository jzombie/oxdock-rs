WHITESPACE = _{ " " | "\t" }
linebreak = _{ "\r\n" | "\n" }
COMMENT = _{ line_comment | block_comment }
line_comment = _{ "//" ~ (!linebreak ~ ANY)* }
block_comment = _{ "/*" ~ (block_comment | !"*/" ~ ANY)* ~ "*/" }

// Hash comments are only valid at the start of a line (possibly indented)
hash_comment = _{ WHITESPACE* ~ "#" ~ (!linebreak ~ ANY)* }

script = { SOI ~ element* ~ EOI }

element = _{ blank | hash_comment | semicolon | guard_line | block_start | block_end | command }

blank = _{ linebreak+ }

semicolon = _{ ";" }

block_start = { "{" }

block_end = { "}" }

guard_line = { "[" ~ ws? ~ guard_groups ~ ws? ~ "]" }

guard_groups = { guard_conjunction ~ ( "|" ~ guard_conjunction )* }

guard_conjunction = { guard_term ~ ( "," ~ guard_term )* }

guard_term = { ws? ~ invert? ~ guard_predicate ~ ws? }

invert = { "!" }

guard_predicate = _{ env_guard | platform_guard | bare_platform }

env_guard = { env_prefix ~ ws? ~ env_key ~ env_comparison? }

env_prefix = { "env" ~ ":" }

env_key = @{ guard_key_char+ }

guard_key_char = _{ !( ":" | "=" | "!" | "," | "|" | "]" | linebreak | "//" | "/*" ) ~ ANY }

env_comparison = { not_equals | equals }

equals = { "=" ~ env_value }

not_equals = { "!=" ~ env_value }

env_value = @{ guard_value_char+ }

guard_value_char = _{ !( "," | "|" | "]" | linebreak | "//" | "/*" ) ~ ANY }

platform_guard = { "platform" ~ (":" | "=") ~ ws? ~ platform_tag }

bare_platform = { platform_tag }

platform_tag = @{ ASCII_ALPHANUMERIC+ }

command = _{ run_command | run_bg_command | simple_command }

run_command = { "RUN" ~ run_args }

run_bg_command = { "RUN_BG" ~ run_args }

run_args = { (quoted_string | unquoted_run_content)+ }

simple_command = { command_name ~ simple_args? }

command_name = @{ (ASCII_ALPHANUMERIC | "_")+ }

simple_args = { (quoted_string | unquoted_simple_content)+ }

quoted_string = @{
    "\"" ~ ( "\\\"" | (!"\"" ~ ANY) )* ~ "\"" |
    "'" ~ ( "\\'" | (!"'" ~ ANY) )* ~ "'"
}

unquoted_run_content = {
    !(linebreak | "//" | "/*") ~ ANY
}

unquoted_simple_content = {
    !(linebreak | ";" | "//" | "/*") ~ ANY
}

ws = _{ (WHITESPACE | linebreak)* }
